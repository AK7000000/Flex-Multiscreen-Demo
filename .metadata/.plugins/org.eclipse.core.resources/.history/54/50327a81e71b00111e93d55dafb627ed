package com.gisinc.showcase.multiscreen.pm
{
	import com.esri.ags.Graphic;
	import com.esri.ags.Map;
	import com.esri.ags.geometry.MapPoint;
	import com.esri.ags.geometry.Polygon;
	import com.gisinc.showcase.multiscreen.event.MappingEvent;
	import com.gisinc.showcase.multiscreen.model.ApplicationModel;
	
	import flash.events.IEventDispatcher;
	import flash.events.MouseEvent;
	import flash.geom.Point;
	
	import mx.events.FlexEvent;
	import mx.utils.object_proxy;
	
	import spark.components.BorderContainer;

	public class ParcelInfoSymbolPM
	{
		public static const HIDDEN_STATE:String = "hidden";
		public static const VISIBLE_STATE:String = "visible";
		
		[Dispatcher]
		public var dispatcher:IEventDispatcher;	
		
		[Bindable]
		public var currentState:String;
		
		[Bindable][Inject("applicationModel.screenType")]
		public var screenType:String;
		
		[Bindable][Inject("mapModel.currentParcel", bind="true")]
		public var currentParcel:Graphic;
		
		[Bindable][Inject("mapModel.map", bind="true")]
		public var map:Map;
		
		private var infoContainer:BorderContainer;
		
		[Bindable][Embed(source='assets/images/close_button.png')]
		public var close_button:Class;
		
		function ParcelInfoSymbolPM()
		{
		}
		
		[PostConstruct]
		public function init():void
		{
			currentState = HIDDEN_STATE;
		}
		
		public function registerInfoContainer(infoContainer:BorderContainer):void
		{
			this.infoContainer = infoContainer;
		}
		
		public function handleCloseButtonClick(event:MouseEvent):void
		{
			currentState = HIDDEN_STATE;	
		}
		
		[EventHandler("MappingEvent.PARCEL_DETAILS_LOADED", properties="parcel")]
		public function handleShowDetails(parcel:Graphic):void
		{
			if(screenType == ApplicationModel.MOBILE_SCREEN_TYPE) return;
			
			currentState = VISIBLE_STATE;
			var infoPoint:Point = map.toScreen((parcel.geometry as Polygon).extent.center);
			
			
			infoContainer.x = infoPoint.x;
			infoContainer.y = infoPoint.y;
		}
	}
}